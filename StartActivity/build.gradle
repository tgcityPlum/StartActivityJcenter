apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //忽略错误信息
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
}

/*publish { // 新增
    repoName  = 'utils' // bintray上的maven name
    userOrg = 'tgcity' // 注册bintray时的username
    groupId = 'com.tgcity.utils' // 项目包名
    artifactId = 'StartActivity' // 项目名
    publishVersion = '1.0.0' // 发布版本号
    desc = 'A utils about activity intent' // 项目描述，可选项
    website = 'https://github.com/tgcityPlum/StartActivityJcenter' // 项目站点，可选项
}*/


def siteUrl = 'https://github.com/tgcityPlum/StartActivityJcenter' // 项目主页
def gitUrl = 'https://github.com/tgcityPlum/StartActivityJcenter.git' // 项目的git地址
def module_name = 'StartActivity' // 项目的名称
group = 'com.tgcity.utils' // 所在组
version = "1.0.3" //aar的版本号

install {
    repositories.mavenInstaller {
// This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'StartActivity' // 名称
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0' // 开源协议名称
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt' // 协议地址
                    }
                }
                developers {
                    developer {
                        id 'tgcity' // 账号
                        name 'tgcity' // 名称
                        email 'tgcityPlum@gmail.com' // 邮箱地址
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
// 读取配置文件中的用户名和key
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "utils" // 你在bintray上创建的库的名称
        name = module_name // 在jcenter中的项目名称
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}